(ns physics-2d.backend-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.physics.contract :as contract]
            [physics-2d.backend :as backend]
            [physics_2d :as engine]))

(defn body [id x mass]
  {:entity/id id :transform/position [x 0.0] :physics/velocity [0.0 0.0]
   :physics/body {:mass mass :restitution 0.5 :collider (engine/make-circle-collider 1.0)}})

(deftest steps-shared-scene-and-preserves-ids
  (let [scene (assoc (contract/make-scene {:id :game :dimensions 2
                                            :entities [(body :a 0.0 1.0) (body :b 1.5 0.0)]})
                     :scene/forces {:gravity [0.0 0.0]})
        stepped (contract/step backend/backend scene (/ 1.0 60.0))]
    (is (= [:a :b] (mapv :entity/id (:scene/entities stepped))))
    (is (= #{[:a :b]} (set (map (juxt :entity-a :entity-b) (:physics/contacts stepped)))))
    (is (contract/supports? backend/backend :realtime #{:rigid-body-2d}))))

(deftest rejects-unsafe-time-and-fidelity-substitution
  (testing "NaN and oversized frame steps fail instead of poisoning the world"
    (is (thrown? #?(:clj Exception :cljs js/Error) (contract/step backend/backend {} ##NaN)))
    (is (thrown? #?(:clj Exception :cljs js/Error) (contract/step backend/backend {} 0.5))))
  (is (not (contract/supports? backend/backend :high-fidelity #{:rigid-body-2d}))))
